AppType=StandardJava
Build1=Default,b4j.example
Group=App
Library1=jcore
Library2=json
Library3=b4xcollections
Module1=Anchor
Module10=Form
Module11=H1
Module12=H2
Module13=H3
Module14=H4
Module15=H5
Module16=H6
Module17=Head
Module18=Html
Module19=Img
Module2=Body
Module20=Input
Module21=Italic
Module22=Label
Module23=Li
Module24=Meta
Module25=MiniHtmlParser
Module26=Ol
Module27=Option
Module28=Paragraph
Module29=Plain
Module3=Bold
Module30=Select
Module31=Span
Module32=Strong
Module33=Table
Module34=Tag
Module35=Tbody
Module36=Td
Module37=Textarea
Module38=Th
Module39=Thead
Module4=Br
Module40=Tr
Module41=Ul
Module42=Underline
Module5=Button
Module6=Del
Module7=Div
Module8=Document
Module9=Em
NumberOfFiles=0
NumberOfLibraries=3
NumberOfModules=42
Version=10.3
@EndOfDesignText@
'Non-UI application (console / server application)
#Region Project Attributes 
	#CommandLineArgs:
	#MergeLibraries: True
	#LibraryName: MiniHtml
	#LibraryVersion: 0.10
	#LibraryAuthor: Aeric
#End Region
#Region Macros
'#Macro: Title, Export as zip, ide://run?file=%B4X%\Zipper.jar&Args=%PROJECT_NAME%.zip
#Macro: Title, Update Version, ide://run?file=%JAVABIN%\java.exe&Args=-jar&Args=%ADDITIONAL%\..\B4X\manifest-writer.jar&Args=%PROJECT%\..\..&Args=%PROJECT%\..\..&Args=Version&Args=3.80
#Macro: Title, Create B4xLib, ide://run?file=%JAVABIN%\jar.exe&WorkingDirectory=..&args=-cMf&args=..\..\release\%PROJECT_NAME%.b4xlib&args=..\*ORM*.bas&args=..\..\manifest.txt&args=..\..\LICENSE
#Macro: Title, Release folder, ide://run?file=%WINDIR%\SysWOW64\explorer.exe&Args=%PROJECT%\..\..\release
#Macro: Title, Copy to AddLibs, ide://run?file=%COMSPEC%&args=/c&args=copy&args=%PROJECT%\..\..\release\*.b4xlib&args=%ADDITIONAL%\..\B4X
'#Macro: Title, Sync, ide://run?file=%WINDIR%\System32\Robocopy.exe&args=..\..\Shared+Files&args=..\Files&FilesSync=True
#End Region
Sub Process_Globals
	Private HtmlParser As MiniHtmlParser
	Public Const COLOR_POST As Int = -29696
End Sub

Sub AppStart (Args() As String)
	HtmlParser.Initialize
	Dim doc As Document
	doc.Initialize
	doc.AppendDocType
	doc.Append(GenerateHtml)
	Dim content As String = doc.ToString
	LogColor(content, -16776961)
	File.WriteString(File.DirApp, "index.html", content)
	StartMessageLoop
End Sub

Sub GenerateHtml As String
	Dim html1 As Tag = Html.lang("en")
	html1.comment(" generated by MiniHTML ")
	Head.up(html1).meta_preset.title("MiniHTML Demo") _
	.linkcss("css/bootstrap.min.css") _
	.script("js/bootstrap.bundle.min.js")
	Dim body1 As Tag = Body.up(html1)
	Dim main1 As Tag = Html.create("main").up(body1).addClass("container").addStyle("margin-top: 30px")
	main1.comment(" Example: https://getbootstrap.com/docs/5.3/examples/heroes/ ")
	Dim div1 As Tag = Div.up(main1).addClass("container")
	Dim div2 As Tag = DIV.up(div1).addClass("row align-items-center py-5")
	Dim col1 As Tag = DIV.up(div2).addClass("col-lg-7 text-center")
	H1.addClass("display-4 fw-bold lh-1 text-body-emphasis mb-3").Text("Vertically centered hero sign-up form").up(col1)
	Paragraph.addClass("col-lg-10 fs-4").Text("Below is an example form built entirely with Bootstrap’s form controls. Each required form group has a validation state that can be triggered by attempting to submit the form without completing it.").up(col1)
	
	Dim loginForm As String = $"<form class="p-4 p-md-5 border rounded-3 bg-body-tertiary">
          <div class="form-floating mb-3">
            <input type="email" class="form-control" id="floatingInput" placeholder="name@example.com">
            <label for="floatingInput">Email address</label>
          </div>
          <div class="form-floating mb-3">
            <input type="password" class="form-control" id="floatingPassword" placeholder="Password">
            <label for="floatingPassword">Password</label>
          </div>
          <div class="checkbox mb-3">
            <label>
              <input type="checkbox" value="remember-me"> Remember me
            </label>
          </div>
          <button class="w-100 btn btn-lg btn-primary" type="submit">Sign up</button>
          <hr class="my-4">
          <small class="text-body-secondary">By clicking Sign up, you agree to the terms of use.</small>
        </form>"$

	Dim root As HtmlNode = HtmlParser.Parse(loginForm)
	If root.IsInitialized Then
		Dim newTag As Tag = ConvertTagNodeToTag(root.Children.Get(0))
		Div.up(div2).addClass("col-lg-5 col-md-10 mx-auto").add(newTag)
	End If
	Return html1.build
End Sub

Sub ConvertTagNodeToTag (node1 As HtmlNode) As Tag
	Dim parent As Tag
	parent.Initialize(node1.Name)
	Dim class1 As String = HtmlParser.GetAttributeValue(node1, "class", "")
	Dim style1 As String = HtmlParser.GetAttributeValue(node1, "style", "")
	
	If class1 <> "" Then parent.addClass(class1)
	If style1 <> "" Then parent.addStyle(style1)
	
	For Each att As HtmlAttribute In node1.Attributes
		If att.Key <> "class" And att.Key <> "style" Then
			If att.Key = "value" And att.Value <> "" Then
				If node1.Name = "input" Then 
					parent.attribute(att.Key, att.Value)
				Else
					parent.Text(att.Value)
				End If
			Else
				parent.attribute(att.Key, att.Value)
			End If
		End If
	Next
	For Each child As HtmlNode In node1.Children
		Dim tag2 As Tag = ConvertTagNodeToTag(child)
		parent.add(tag2)
	Next
	Return parent
End Sub